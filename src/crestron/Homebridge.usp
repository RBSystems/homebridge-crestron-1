/*******************************************************************************************
  SIMPL+ Module Information
  (Fill in comments below)
*******************************************************************************************/
/*
Dealer Name:
System Name:
System Number:
Programmer: Jonathan Avila
Comments:
*/

/*******************************************************************************************
  Compiler Directives
  (Uncomment and declare compiler directives as needed)
*******************************************************************************************/

#SYMBOL_NAME "Homebridge"
#HINT "This program communicates with a homebridge server to enable Crestron compatibility with Apple HomeKit."
#PRINT_TO_TRACE
#DEFAULT_VOLATILE
#ENABLE_STACK_CHECKING
#ENABLE_TRACE

/*******************************************************************************************
  Include Libraries
  (Uncomment and include additional libraries as needed)

  Note:  Crestron does not recommend the use of the #INCLUDEPATH directive in SIMPL+ modules
  that are to be used in Crestron Studio.
*******************************************************************************************/

#USER_SIMPLSHARP_LIBRARY "SSharpHomebridge"


/*******************************************************************************************
  DIGITAL, ANALOG and SERIAL INPUTS and OUTPUTS
  (Uncomment and declare inputs and outputs as needed)
*******************************************************************************************/
DIGITAL_INPUT Light_IsOn[50];
DIGITAL_INPUT _SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_;
DIGITAL_INPUT DimLight_IsOn[50];
DIGITAL_INPUT _SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_;
DIGITAL_INPUT Switch_IsOn[50];
DIGITAL_INPUT _SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_;

ANALOG_INPUT DimLight_Level[50];
ANALOG_INPUT _SKIP_;

DIGITAL_OUTPUT Light_On[50];
DIGITAL_OUTPUT Light_Off[50];
DIGITAL_OUTPUT DimLight_On[50];
DIGITAL_OUTPUT DimLight_Off[50];
DIGITAL_OUTPUT Switch_On[50];
DIGITAL_OUTPUT Switch_Off[50];
DIGITAL_OUTPUT _SKIP_;

ANALOG_OUTPUT DimLight_Set[50];
ANALOG_OUTPUT _SKIP_;

/*******************************************************************************************
  SOCKETS
  (Uncomment and define socket definitions as needed)
*******************************************************************************************/

TCP_SERVER TCPServer[65000];

/*******************************************************************************************
  Parameters
  (Uncomment and declare parameters as needed)
*******************************************************************************************/

INTEGER_PARAMETER Port;
// SIGNED_INTEGER_PARAMETER
// LONG_INTEGER_PARAMETER
// SIGNED_LONG_INTEGER_PARAMETER
STRING_PARAMETER IPAddress[16];

/*******************************************************************************************
  Parameter Properties
  (Uncomment and declare parameter properties as needed)
*******************************************************************************************/

#BEGIN_PARAMETER_PROPERTIES IPAddress
	propValidUnits = unitString;
	propDefaultValue = "0.0.0.0";
	propShortDescription = "IP Address";
	#BEGIN_PROP_FULL_DESCRIPTION "TCP server's IP Address to allow connections from" #END_PROP_FULL_DESCRIPTION
#END_PARAMETER_PROPERTIES

#BEGIN_PARAMETER_PROPERTIES Port
	propValidUnits = unitDecimal;
	propDefaultValue = 9050d;
	propShortDescription = "port";
	#BEGIN_PROP_FULL_DESCRIPTION "TCP's server port to connect to" #END_PROP_FULL_DESCRIPTION
#END_PARAMETER_PROPERTIES

/*******************************************************************************************
  Global Variables
  (Uncomment and declare global variables as needed)
  Note:  Be sure to initialize all declared STRING variables as needed
         For example, in Function Main: myString = "";
*******************************************************************************************/

SIGNED_INTEGER doServerConnected;
STRING InternalRxBuffer[65000];
MessageBroker Broker;

/*******************************************************************************************
  Functions
  (Add any additional functions here)
  Note:  Functions must be physically placed before the location in
         the code that calls them.
*******************************************************************************************/

Function StartServer()
{
   	SIGNED_INTEGER Status;
	Status = SocketServerStartListen(TCPServer, IPAddress, Port);

	if (status < 0) {
		Print("Error listening to %s on port %u (status: %d)\n", IPAddress, Port, Status);
	} else {
        Print("Server started to %s on port %u (status: %d)\n", IPAddress, Port, status);
	}
}

Function SocketTx(STRING ServerTx)
{
	SIGNED_INTEGER Status;

    Status = SocketSend(TCPServer, ServerTx);

    if (Status < 0) {
		Print("Error Sending to TCPServer: %d\n", Status);
    } else {
		//Print("Sent %s\n", ServerTx);
	}
}

Function HandleLightSwitchMessage(STRING MessageType, INTEGER DeviceId, STRING Operation, STRING Property, INTEGER Value)
{
	INTEGER IsOn;
	STRING ServerTx[500];
	IsOn = Light_IsOn[DeviceId];

	if (MessageType = "Request" && Operation = "Get" && Property = "Power")
	{
		Value = IsOn;
		Broker.Message.Value = Value;
		Broker.Message.MessageType = "Response";
		ServerTx = Broker.SerializeMessage() + "||";
		SocketTx(ServerTx);
	}

	if (MessageType = "Request" && Operation = "Set" && Property = "Power")
	{
		Value = Broker.Message.Value;

		if (Value = 1 && IsOn = 0)
		{
			PULSE(1, Light_On[DeviceId]);
		}
		if (Value = 0 && IsOn = 1)
		{
			PULSE(1, Light_Off[DeviceId]);
		}
	}

	if (MessageType = "Event" && Operation = "Set" && Property = "Power")
	{
		ServerTx = Broker.SerializeMessage() + "||";
		SocketTx(ServerTx);
	}
}

Function HandleLightDimmerMessage(STRING MessageType, INTEGER DeviceId, STRING Operation, STRING Property, INTEGER Value)
{
	INTEGER IsOn;
	INTEGER Level;
	STRING ServerTx[500];
	IsOn = DimLight_IsOn[DeviceId];
	Level = DimLight_Level[DeviceId];

	if (MessageType = "Request" && Operation = "Get" && Property = "Power")
	{
		Value = IsOn;
		Broker.Message.Value = Value;
		Broker.Message.MessageType = "Response";
		ServerTx = Broker.SerializeMessage() + "||";
		SocketTx(ServerTx);
	}

	if (MessageType = "Request" && Operation = "Get" && Property = "Level")
	{
		Value = Level;
		Broker.Message.Value = Value;
		Broker.Message.MessageType = "Response";
		ServerTx = Broker.SerializeMessage() + "||";
		SocketTx(ServerTx);
	}

	if (MessageType = "Request" && Operation = "Set" && Property = "Power")
	{
		Value = Broker.Message.Value;

		if (Value = 1 && IsOn = 0)
		{
			PULSE(1, DimLight_On[DeviceId]);
		}

		if (Value = 0 && IsOn = 1)
		{
			PULSE(1, DimLight_Off[DeviceId]);
		}
	}

	if (MessageType = "Request" && Operation = "Set" && Property = "Level")
	{
		Value = Broker.Message.Value;

		if (Value = 65535)
		{
			PULSE(1, DimLight_On[DeviceId]);
		}
		else
		{
			DimLight_Set[DeviceId] = Value;
		}
	}

	if (MessageType = "Event" && Operation = "Set" && Property = "Power")
	{
		ServerTx = Broker.SerializeMessage() + "||";
		SocketTx(ServerTx);
	}

	if (MessageType = "Event" && Operation = "Set" && Property = "Level")
	{
		ServerTx = Broker.SerializeMessage() + "||";
		SocketTx(ServerTx);
	}
}

Function HandleGenericSwitchMessage(STRING MessageType, INTEGER DeviceId, STRING Operation, STRING Property, INTEGER Value)
{
	INTEGER IsOn;
	STRING ServerTx[500];
	IsOn = Switch_IsOn[DeviceId];

	if (MessageType = "Request" && Operation = "Get" && Property = "Power")
	{
		Value = IsOn;
		Broker.Message.Value = Value;
		Broker.Message.MessageType = "Response";
		ServerTx = Broker.SerializeMessage() + "||";
		SocketTx(ServerTx);
	}

	if (MessageType = "Request" && Operation = "Set" && Property = "Power")
	{
		Value = Broker.Message.Value;

		if (Value = 1 && IsOn = 0)
		{
			PULSE(1, Switch_On[DeviceId]);
		}
		if (Value = 0 && IsOn = 1)
		{
			PULSE(1, Switch_Off[DeviceId]);
		}
	}

	if (MessageType = "Event" && Operation = "Set" && Property = "Power")
	{
		ServerTx = Broker.SerializeMessage() + "||";
		SocketTx(ServerTx);
	}
}

Function ProcessMessage(STRING MessageJson)
{
	INTEGER DeviceId;
	STRING DeviceType[50];
	STRING MessageType[50];
	STRING Operation[50];
	STRING Property[50];
	INTEGER Value;

	Broker.ParseMessage(MessageJson);
	DeviceId = Broker.Message.DeviceId;
	DeviceType = Broker.Message.DeviceType;
	MessageType = Broker.Message.MessageType;
	Operation = Broker.Message.Operation;
	Property = Broker.Message.Property;

	if (DeviceType = "LightSwitch")
	{
		HandleLightSwitchMessage(MessageType, DeviceId, Operation, Property, Value);
	}

	if (DeviceType = "LightDimmer")
	{
		HandleLightDimmerMessage(MessageType, DeviceId, Operation, Property, Value);
	}

	if (DeviceType = "GenericSwitch")
	{
		HandleGenericSwitchMessage(MessageType, DeviceId, Operation, Property, Value);
	}

}

Function ChangeMessageOnEvent(STRING MessageType, STRING DeviceType, INTEGER DeviceId, STRING Operation, STRING Property, INTEGER Value)
{
	Broker.Message.MessageType = MessageType;
	Broker.Message.DeviceType = DeviceType;
	Broker.Message.DeviceId = DeviceId;
	Broker.Message.Operation = Operation;
	Broker.Message.Property = Property;
	Broker.Message.Value = Value;
}

Function SocketRx()
{
	// Print("InternalRxBuffer: %s\n", InternalRxBuffer);
	STRING Temp$[65000];
    WHILE (LEN(InternalRxBuffer) > 1) {
		Temp$ = REMOVE("||",InternalRxBuffer);
    	Temp$ = LEFT(Temp$, (Len(Temp$)-2));
		ProcessMessage(Temp$);
	}
}

/*******************************************************************************************
  Event Handlers
  (Uncomment and declare additional event handlers as needed)
*******************************************************************************************/

CHANGE Light_IsOn
{
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	ChangeMessageOnEvent("Event", "LightSwitch", Id, "Set", "Power", Light_IsOn[Id]);
	HandleLightSwitchMessage(Broker.Message.MessageType, Broker.Message.DeviceId, Broker.Message.Operation, Broker.Message.Property, Broker.Message.Value);
}

CHANGE DimLight_IsOn
{
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	ChangeMessageOnEvent("Event", "LightDimmer", Id, "Set", "Power", DimLight_IsOn[Id]);
	HandleLightDimmerMessage(Broker.Message.MessageType, Broker.Message.DeviceId, Broker.Message.Operation, Broker.Message.Property, Broker.Message.Value);
}

CHANGE DimLight_Level
{
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	ChangeMessageOnEvent("Event", "LightDimmer", Id, "Set", "Level", DimLight_Level[Id]);
	HandleLightDimmerMessage(Broker.Message.MessageType, Broker.Message.DeviceId, Broker.Message.Operation, Broker.Message.Property, Broker.Message.Value);
}

CHANGE Switch_IsOn
{
	INTEGER Id;
	Id = GetLastModifiedArrayIndex();
	ChangeMessageOnEvent("Event", "GenericSwitch", Id, "Set", "Power", Switch_IsOn[Id]);
	HandleGenericSwitchMessage(Broker.Message.MessageType, Broker.Message.DeviceId, Broker.Message.Operation, Broker.Message.Property, Broker.Message.Value);
}

SOCKETCONNECT TCPServer
{
    doServerConnected = 1;
    Print("OnConnect: input buffer size is: %d\n", Len(TCPServer.SocketRxBuf));
}



SOCKETDISCONNECT TCPServer
{
    doServerConnected = 0;
}



SOCKETRECEIVE TCPServer
{
    InternalRxBuffer = InternalRxBuffer + TCPServer.SocketRxBuf;
	ClearBuffer(TCPServer.SocketRxBuf);
	SocketRx();
}



SOCKETSTATUS TCPServer
{
    SIGNED_INTEGER Status;
    Status = SocketGetStatus();
    Print("The SocketGetStatus returns:       %d\n", Status);
    Print("The TCPServer.SocketStatus returns: %d\n", TCPServer.SocketStatus);
}


/*******************************************************************************************
  Main()
  Uncomment and place one-time startup code here
  (This code will get called when the system starts up)
*******************************************************************************************/

Function Main()
{
    WaitForInitializationComplete();
    // If you are reading any Input or Output variables, uncomment
    //   the WaitForInitializationComplete statement above and read
    //   them afterwards.  Input/Output variables will not have
    //   their correct values set until after the logic processor
    //   runs and propagates the values to them.

	//Initialize the TCP server
	StartServer();
    Print("Program started!!!.\n");
}


